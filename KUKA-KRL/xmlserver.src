&ACCESS RVP
&REL 1
&PARAM SensorITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program
DEF XmlServer( )
;FOLD Declaration
  DECL EKI_STATUS RET
  DECL E6POS DefaultPos
  DECL E6POS CognexPos
  DECL REAL CognexX
  DECL REAL CognexY
  DECL REAL CognexA
  DECL E6AXIS HOME
;ENDFOLD (Declaration)
;FOLD INI
  ;FOLD BASISTECH INI
  BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here
  CognexX = 0; muss einmal gesetzt werden, sonst in EKI_GetReal nicht nutzbar
  CognexY = 0; muss einmal gesetzt werden, sonst in EKI_GetReal nicht nutzbar
  CognexA = 0; muss einmal gesetzt werden, sonst in EKI_GetReal nicht nutzbar
  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

BAS(#TOOL,0)
BAS(#BASE,0)

RET = EKI_Init("XmlServer")
RET = EKI_Lock("XmlServer") ; discard any input by default (does not seem to help)

SPTP XHOME 
DefaultPos = $POS_ACT

while True

RET = EKI_Open("XmlServer")

; wait until server is conntected
wait for $FLAG[1]

;LIN_REL {Z 10}

while $FLAG[1]

;FOLD receive from external program
; <Cognex><X>0</X><Y>0</Y><A>0</A></Cognex>
;ENDFOLD (Receive from external program)
   
RET = EKI_Unlock("XmlServer") ; enable reciever

; wait until Cognex element was received or disconnect happened
wait for $FLAG[3]

if $FLAG[2] then
   RET = EKI_Lock("XmlServer") ; discard any input while moving 

   $FLAG[2] = FALSE ; reset "data received"

   RET = EKI_GetReal("XmlServer", "Cognex/X", CognexX)
   RET = EKI_GetReal("XmlServer", "Cognex/Y", CognexY)
   RET = EKI_GetReal("XmlServer", "Cognex/A", CognexA)
   
   ; poor man's relative movement
   CognexPos = DefaultPos  
   CognexPos.X = DefaultPos.X + CognexX
   CognexPos.Y = DefaultPos.Y + CognexY
   CognexPos.A = DefaultPos.A + CognexA

   debug_e6pos = CognexPos
   
   RET = EKI_ClearBuffer("XmlServer", "Cognex") ; clear buffer (does not seem to help)
   
   SLIN CognexPos
endif

endwhile

RET = EKI_Close("XmlServer") ; TODO: suppress error message

endwhile

RET = EKI_Clear("XmlServer")

END
